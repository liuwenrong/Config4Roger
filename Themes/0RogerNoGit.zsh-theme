# vim:ft=zsh ts=2 sw=2 sts=2
#
# agnoster's Theme - https://gist.github.com/3712874
# A Powerline-inspired theme for ZSH
#
# # README
#
# In order for this theme to render correctly, you will need a
# [Powerline-patched font](https://github.com/Lokaltog/powerline-fonts).
# Make sure you have a recent version: the code points that Powerline
# uses changed in 2012, and older versions will display incorrectly,
# in confusing ways.
#
# In addition, I recommend the
# [Solarized theme](https://github.com/altercation/solarized/) and, if you're
# using it on Mac OS X, [iTerm 2](http://www.iterm2.com/) over Terminal.app -
# it has significantly better color fidelity.
#
# # Goals
#
# The aim of this theme is to only show you *relevant* information. Like most
# prompts, it will only show git information when in a git working directory.
# However, it goes a step further: everything from the current user and
# hostname to whether the last call exited with an error to whether background
# jobs are running in this shell will all be displayed automatically when
# appropriate.

### Segment drawing
# A few utility functions to make it easy and re-usable to draw segmented prompts

CURRENT_BG='NONE'

# Special Powerline characters

() {
  local LC_ALL="" LC_CTYPE="en_US.UTF-8"
  # NOTE: This segment separator character is correct.  In 2012, Powerline changed
  # the code points they use for their special characters. This is the new code point.
  # If this is not working for you, you probably have an old version of the
  # Powerline-patched fonts installed. Download and install the new version.
  # Do not submit PRs to change this unless you have reviewed the Powerline code point
  # history and have new information.
  # This is defined using a Unicode escape sequence so it is unambiguously readable, regardless of
  # what font the user is viewing this source code in. Do not replace the
  # escape sequence with a single literal character.
  # Do not change this! Do not make it '\u2b80'; that is the old, wrong code point.
  SEGMENT_SEPARATOR=$'\ue0b0'
}

# Begin a segment
# Takes two arguments, background and foreground. Both can be omitted,
# rendering default background/foreground.
prompt_segment() {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $CURRENT_BG != 'NONE' && $1 != $CURRENT_BG ]]; then
    echo -n " %{$bg%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR%{$fg%} "
  else
    echo -n "%{$bg%}%{$fg%} "
  fi
  CURRENT_BG=$1
  [[ -n $3 ]] && echo -n $3
}

# End the prompt, closing any open segments
prompt_end() {
  if [[ -n $CURRENT_BG ]]; then
    echo -n " %{%k%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR"
  else
    echo -n "%{%k%}"
  fi
  echo -n "%{%f%}"
  CURRENT_BG=''
}

### Prompt components
# Each component will draw itself, and hide itself if no information needs to be shown

# Context: user@hostname (who am I and where am I)
prompt_context() {
  #if [[ "$USER" != "$DEFAULT_USER" || -n "$SSH_CLIENT" ]]; then
    #prompt_segment black default "%(!.%{%F{yellow}%}.)$USER@%m"
  #fi
    prompt_segment black default "%(!.%{%F{yellow}%}.)‚òÅRoger‚ô£liuwenrong"
}

# Git: branch/detached head, dirty status
prompt_git() {
  (( $+commands[git] )) || return
  local PL_BRANCH_CHAR
  () {
    local LC_ALL="" LC_CTYPE="en_US.UTF-8"
    PL_BRANCH_CHAR=$'\ue0a0'         # ÓÇ†
  }
  local ref dirty mode repo_path
  repo_path=$(git rev-parse --git-dir 2>/dev/null)

  if $(git rev-parse --is-inside-work-tree >/dev/null 2>&1); then
    dirty=$(parse_git_dirty)
    ref=$(git symbolic-ref HEAD 2> /dev/null) || ref="‚û¶ $(git rev-parse --short HEAD 2> /dev/null)"
    if [[ -n $dirty ]]; then
      prompt_segment yellow black
    else
      prompt_segment green black
    fi

    if [[ -e "${repo_path}/BISECT_LOG" ]]; then
      mode=" <B>"
    elif [[ -e "${repo_path}/MERGE_HEAD" ]]; then
      mode=" >M<"
    elif [[ -e "${repo_path}/rebase" || -e "${repo_path}/rebase-apply" || -e "${repo_path}/rebase-merge" || -e "${repo_path}/../.dotest" ]]; then
      mode=" >R>"
    fi

    setopt promptsubst
    autoload -Uz vcs_info

    zstyle ':vcs_info:*' enable git
    zstyle ':vcs_info:*' get-revision true
    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' stagedstr '‚úö'
    zstyle ':vcs_info:*' unstagedstr '‚óè'
    zstyle ':vcs_info:*' formats ' %u%c'
    zstyle ':vcs_info:*' actionformats ' %u%c'
    vcs_info
    echo -n "${ref/refs\/heads\//$PL_BRANCH_CHAR }${vcs_info_msg_0_%% }${mode}"
  fi
}

prompt_bzr() {
    (( $+commands[bzr] )) || return
    if (bzr status >/dev/null 2>&1); then
        status_mod=`bzr status | head -n1 | grep "modified" | wc -m`
        status_all=`bzr status | head -n1 | wc -m`
        revision=`bzr log | head -n2 | tail -n1 | sed 's/^revno: //'`
        if [[ $status_mod -gt 0 ]] ; then
            prompt_segment yellow black
            echo -n "bzr@"$revision "‚úö "
        else
            if [[ $status_all -gt 0 ]] ; then
                prompt_segment yellow black
                echo -n "bzr@"$revision

            else
                prompt_segment green black
                echo -n "bzr@"$revision
            fi
        fi
    fi
}

prompt_hg() {
  (( $+commands[hg] )) || return
  local rev status
  if $(hg id >/dev/null 2>&1); then
    if $(hg prompt >/dev/null 2>&1); then
      if [[ $(hg prompt "{status|unknown}") = "?" ]]; then
        # if files are not added
        prompt_segment red white
        st='¬±'
      elif [[ -n $(hg prompt "{status|modified}") ]]; then
        # if any modification
        prompt_segment yellow black
        st='¬±'
      else
        # if working copy is clean
        prompt_segment green black
      fi
      echo -n $(hg prompt "‚òø {rev}@{branch}") $st
    else
      st=""
      rev=$(hg id -n 2>/dev/null | sed 's/[^-0-9]//g')
      branch=$(hg id -b 2>/dev/null)
      if `hg st | grep -q "^\?"`; then
        prompt_segment red black
        st='¬±'
      elif `hg st | grep -q "^[MA]"`; then
        prompt_segment yellow black
        st='¬±'
      else
        prompt_segment green black
      fi
      echo -n "‚òø $rev@$branch" $st
    fi
  fi
}

# Dir: current working directory
prompt_dir() {
  prompt_segment blue black '%~'
}

# Virtualenv: current working virtualenv
prompt_virtualenv() {
  local virtualenv_path="$VIRTUAL_ENV"
  if [[ -n $virtualenv_path && -n $VIRTUAL_ENV_DISABLE_PROMPT ]]; then
    prompt_segment blue black "(`basename $virtualenv_path`)"
  fi
}

# Status:
# - was there an error
# - am I root
# - are there background jobs?
prompt_status() {
  local symbols
  symbols=()
  [[ $RETVAL -ne 0 ]] && symbols+="%{%F{red}%}‚úò"
  [[ $UID -eq 0 ]] && symbols+="%{%F{yellow}%}‚ö°"
  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{cyan}%}‚öô"

  [[ -n "$symbols" ]] && prompt_segment black default "$symbols"
}

# Dir: current working directory
prompt_time() {
 #"%Y-%m-%d %I:%M:%S
 #"%D{"%Y-%m-%d %I:%M:%S"}"
 #prompt_segment green black '%Y-%m-%d‚ôÜ%I:%M:%S'  #ÊòæÁ§∫Ë∑ØÂæÑ %sÊúâÈóÆÈ¢ò
 prompt_segment white black '‚òÇ %D{%Y-%m-%d %X}'  #2018-11-18 11:18:18
 #prompt_segment green black '%D{[%X]}'  #[11:18:18]
 #prompt_segment green black '‚ôÜ%t'
 #prompt_segment green black '%W‚ôÜ%t'
# Grab the current date (%W) and time (%t):
#Roger_TIME_="%{$fg_bold[red]%}#%{$fg_bold[white]%}( %{$fg_bold[yellow]%}%W%{$reset_color%}@%{$fg_bold[white]%}%t )( %{$reset_color%}"
}

## Main prompt
build_prompt() {
  RETVAL=$?
  prompt_status
  prompt_dir
  #prompt_git
  prompt_bzr
  prompt_hg
  prompt_end
}

# secondary line prompt
build_prompt1() {
  prompt_virtualenv
  prompt_context
  prompt_time
  prompt_end
}

#PROMPT='‚ï≠‚îÄ%{%f%b%k%}$(build_prompt) ‚ûú $(git_prompt_status)
PROMPT='‚ï≠‚îÄ%{%f%b%k%}$(build_prompt1)
‚ï∞‚îÄ%{$fg[$CARETCOLOR]%}‚û§ %{%f%b%k%}$(build_prompt)'
#‚ï∞‚îÄ%{$fg[$CARETCOLOR]%}‚û§ %{%f%b%k%}$(build_prompt) %{$resetcolor%}'
#‚ï∞‚îÄ%{$fg[$CARETCOLOR]%}‚ñ∂%{$resetcolor%} '‚û§
#
ZSH_THEME_GIT_PROMPT_PREFIX=" ‚òÅ  %{$fg[red]%}"
ZSH_THEME_GIT_PROMPT_SUFFIX="%{$reset_color%}"
ZSH_THEME_GIT_PROMPT_DIRTY="%{$fg[yellow]%} ‚òÇ" # ‚íπ
ZSH_THEME_GIT_PROMPT_UNTRACKED="%{$fg[cyan]%} ‚ú≠" # ‚ì£
ZSH_THEME_GIT_PROMPT_CLEAN="%{$fg[green]%} ‚òÄ" # ‚ìÑ

ZSH_THEME_GIT_PROMPT_ADDED="%{$fg[cyan]%} ‚úö" # ‚ìê ‚ëÉ
ZSH_THEME_GIT_PROMPT_MODIFIED="%{$fg[yellow]%} ‚ö°"  # ‚ìú ‚ëÅ
ZSH_THEME_GIT_PROMPT_DELETED="%{$fg[red]%} ‚úñ" # ‚ìß ‚ëÇ
ZSH_THEME_GIT_PROMPT_RENAMED="%{$fg[blue]%} ‚ûú" # ‚ì° ‚ëÑ
ZSH_THEME_GIT_PROMPT_UNMERGED="%{$fg[magenta]%} ‚ôí" # ‚ì§ ‚ëä
ZSH_THEME_GIT_PROMPT_AHEAD="%{$fg[blue]%} ùùô"

# More symbols to choose from:
# ‚òÄ ‚úπ ‚òÑ ‚ôÜ ‚ôÄ ‚ôÅ ‚ôê ‚ôá ‚ôà ‚ôâ ‚ôö ‚ôõ ‚ôú ‚ôù ‚ôû ‚ôü ‚ô† ‚ô£ ‚ö¢ ‚ö≤ ‚ö≥ ‚ö¥ ‚ö• ‚ö§ ‚ö¶ ‚öí ‚öë ‚öê ‚ô∫ ‚ôª ‚ôº ‚ò∞ ‚ò± ‚ò≤ ‚ò≥ ‚ò¥ ‚òµ ‚ò∂ ‚ò∑
# ‚ú° ‚úî ‚úñ ‚úö ‚ú± ‚ú§ ‚ú¶ ‚ù§ ‚ûú ‚ûü ‚ûº ‚úÇ ‚úé ‚úê ‚®Ä ‚®Å ‚®Ç ‚®ç ‚®é ‚®è ‚®∑ ‚©ö ‚©õ ‚©° ‚©± ‚©≤ ‚©µ  ‚©∂ ‚®† 
# ‚¨Ö ‚¨Ü ‚¨á ‚¨à ‚¨â ‚¨ä ‚¨ã ‚¨í ‚¨ì ‚¨î ‚¨ï ‚¨ñ ‚¨ó ‚¨ò ‚¨ô ‚¨ü  ‚¨§ „Äí «Ä «Å «Ç ƒ≠ ≈§ ≈¶



# The right-hand prompt
#RPROMPT='${time} %{$fg[magenta]%}$(git_prompt_info)%{$reset_color%}$(git_prompt_status)%{$reset_color%}$(git_prompt_ahead)%{$reset_color%}'
